plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.diffplug.spotless' version '5.15.0'
}

group = 'org.4germany'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

test {
    useJUnitPlatform {
        excludeTags 'integration'
    }
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    // So that running integration test require running unit tests first,
    // and we won't even attempt running integration tests when there are
    // failing unit tests.
    dependsOn test
    useJUnitPlatform {
        includeTags 'integration'
    }
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

check.dependsOn integrationTest

spotless {
    java {
        removeUnusedImports()
        googleJavaFormat()
    }
    format 'markdown', {
        prettier()
        target '**/*.md'
    }
}
